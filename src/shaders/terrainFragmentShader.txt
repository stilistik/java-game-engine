#version 400 core

in vec2 pass_textureCoords;
in vec3 surfaceNormal;
in vec3 toLightVector[6];
in vec3 toCameraVector;
in float visibility;

out vec4 out_color;

uniform sampler2D backgroundTexture;
uniform sampler2D rTexture;
uniform sampler2D gTexture;
uniform sampler2D bTexture;
uniform sampler2D blendMap;


uniform vec3 lightColor[6];
uniform float shineDamper;
uniform float reflectivity;
uniform vec3 skyColor;

void main(void){

	vec4 blendMapColor = texture(blendMap, pass_textureCoords);
	float backgroundAmount = 1 - (blendMapColor.r + blendMapColor.g + blendMapColor.b);
	vec2 tiledTexCoords = pass_textureCoords * 40.0;
	vec4 backgroundTextureColor = texture(backgroundTexture, tiledTexCoords) * backgroundAmount;
	vec4 rTextureColor = texture(rTexture, tiledTexCoords) * blendMapColor.r;
	vec4 gTextureColor = texture(gTexture, tiledTexCoords) * blendMapColor.g;
	vec4 bTextureColor = texture(bTexture, tiledTexCoords) * blendMapColor.b;
	vec4 totalColor = backgroundTextureColor + rTextureColor + gTextureColor + bTextureColor;
	

	vec3 unitNormal = normalize(surfaceNormal);
	vec3 unitToCameraVector = normalize(toCameraVector);
	
	vec3 totalDiffuse = vec3(0.0);
	vec3 totalSpecular = vec3(0.0);
	
	for (int i = 0; i < 6; i++){
		vec3 unitLightVector = normalize(toLightVector[i]);
		float nDotL = dot(unitNormal, unitLightVector);
		float brightness = max(nDotL, 0.0);
		vec3 lightDirection = -unitLightVector;
		vec3 reflectedDirection = reflect(lightDirection, unitNormal);
		float specularFactor = dot(reflectedDirection, unitToCameraVector);
		specularFactor = max(specularFactor, 0.0);
		float dampedFactor = pow(specularFactor, shineDamper);
		vec3 diffuse = brightness * lightColor[i];
		vec3 specular = dampedFactor * reflectivity * lightColor[i];
		totalDiffuse = totalDiffuse + diffuse;
		totalSpecular = totalSpecular + specular;
	}
	totalDiffuse = max(totalDiffuse, 0.2);
	
	out_color = vec4(totalDiffuse, 1.0) * totalColor + vec4(totalSpecular, 1.0);
	out_color = mix(vec4(skyColor, 1.0), out_color, visibility);
	
}